#include "data/shader/smoke/smokeHeader.glsl"

layout(local_size_x = 16, local_size_y = 16) in;

void main()
{
    ivec2 id = ivec2(gl_GlobalInvocationID.xy);

    float h2 = 0.5f * h;
    float s = loadField(id.x, id.y, S_FIELD);

    float u = loadField(id.x, id.y, U_FIELD);
    float v = loadField(id.x, id.y, V_FIELD);
    
    // u-component
    if (s != 0.f && loadField(id.x - 1, id.y, S_FIELD) != 0.f && id.y < gridResolution.y - 1) {
        float x = id.x * h;
        float y = id.y * h + h2;
        float avgV = avgV(id.x, id.y);
        // float avgV = sampleField(x, y, V_FIELD);
        x -= dt * u;
        y -= dt * avgV;
        u = sampleField(x, y, U_FIELD);   
    }
    // v-component
    if (s != 0.f && loadField(id.x, id.y - 1, S_FIELD) != 0.f && id.x < gridResolution.x - 1) {
        float x = id.x * h + h2;
        float y = id.y * h;
        float avgU = avgU(id.x, id.y);
        // float avgU = sampleField(x, y, U_FIELD);
        x -= dt * avgU;
        y -= dt * v;
        v = sampleField(x, y, V_FIELD);    
    }

    saveField(id.x, id.y, NEXT_U_FIELD, u);
    saveField(id.x, id.y, NEXT_V_FIELD, v);
}