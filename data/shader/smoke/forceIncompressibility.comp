#include "data/shader/smoke/smokeHeader.glsl"

layout(local_size_x = 16, local_size_y = 16) in;

void main()
{
    ivec2 id = ivec2(gl_GlobalInvocationID.xy);

    /*      x0  x1  x2
     *     .---.---.---.
     * y0  | 1 | 0 | 1 |
     *     |---|---|---|
     * y1  | 0 | 1 | 0 | ...
     *     |---|---|---|
     * y2  | 1 | 0 | 1 |
     *     *---*---*---*
     *       ...
     */
    // Alternate between iterations to process in checkboard pattern
    if ( ((id.x + id.y) % 2) == currentIteration % 2 ) {
        return;
    }

    if (loadField(id.x, id.y, S_FIELD) == 0.f) {
        return;
    }
    float prevS = loadField(id.x - 1, id.y, S_FIELD);
    float nextS = loadField(id.x + 1, id.y, S_FIELD);
    float upperS = loadField(id.x, id.y - 1, S_FIELD);
    float lowerS = loadField(id.x, id.y + 1, S_FIELD);
    float s = prevS + nextS + lowerS + upperS;
    if (s == 0.f) {
        return;
    }

    float u1 = loadField(id.x, id.y, U_FIELD);
    float u2 = loadField(id.x + 1, id.y, U_FIELD);
    float v1 = loadField(id.x, id.y, V_FIELD);
    float v2 = loadField(id.x, id.y + 1, V_FIELD);
    float d = u2 - u1 + v2 - v1; // outflow

    float tmp = -d / s;
    tmp *= overrelaxation;

    float cp = density * h / dt;
    float p = loadField(id.x, id.y, P_FIELD);
    float frac = 1.f / (currentIteration + 1);
    saveField(id.x, id.y, P_FIELD, frac * cp * tmp + (1 - frac) * p);

    saveField(id.x, id.y, U_FIELD, u1 - prevS * tmp);
    saveField(id.x + 1, id.y, U_FIELD, u2 + nextS * tmp);
    saveField(id.x, id.y, V_FIELD, v1 - upperS * tmp);
    saveField(id.x, id.y + 1, V_FIELD, v2 + lowerS * tmp);
}