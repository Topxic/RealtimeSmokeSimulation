#include "smokeHeader.glsl"

layout(local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_X, local_size_z = LOCAL_SIZE_Z) in;

void main()
{
    ivec3 id = ivec3(gl_GlobalInvocationID.xyz);

    /*      x0  x1  x2
     *     .---.---.---.
     * y0  | 1 | 0 | 1 |
     *     |---|---|---|
     * y1  | 0 | 1 | 0 | ...
     *     |---|---|---|
     * y2  | 1 | 0 | 1 |
     *     *---*---*---*
     *       ...
     */
    // Alternate between iterations to process in checkboard pattern
    if ( ((id.x + id.y + id.z) % 2) == currentIteration % 2 ) {
        return;
    }

    if (loadField(id.x, id.y, id.z, S_FIELD) == 0.f) {
        return;
    }
    float prevS = loadField(id.x - 1, id.y, id.z, S_FIELD);
    float nextS = loadField(id.x + 1, id.y, id.z, S_FIELD);
    float upperS = loadField(id.x, id.y - 1, id.z, S_FIELD);
    float lowerS = loadField(id.x, id.y + 1, id.z, S_FIELD);
    float frontS = loadField(id.x, id.y, id.z - 1, S_FIELD);
    float backS = loadField(id.x, id.y, id.z + 1, S_FIELD);
    float s = prevS + nextS + lowerS + upperS + frontS + backS;
    if (s == 0.f) {
        return;
    }

    float u1 = loadField(id.x, id.y, id.z, U_FIELD);
    float u2 = loadField(id.x + 1, id.y, id.z, U_FIELD);
    float v1 = loadField(id.x, id.y, id.z, V_FIELD);
    float v2 = loadField(id.x, id.y + 1, id.z, V_FIELD);
    float w1 = loadField(id.x, id.y, id.z, W_FIELD);
    float w2 = loadField(id.x, id.y, id.z + 1, W_FIELD);
    float d = u2 - u1 + v2 - v1 + w2 - w1; // outflow

    float tmp = -d / s;
    tmp *= overrelaxation;

    float cp = density * gridSpacing / dt;
    float p = loadField(id.x, id.y, id.z, P_FIELD);
    float frac = 1.f / (currentIteration + 1);
    saveField(id.x, id.y, id.z, P_FIELD, frac * cp * tmp + (1 - frac) * p);

    saveField(id.x, id.y, id.z, U_FIELD, u1 - prevS * tmp);
    saveField(id.x + 1, id.y, id.z, U_FIELD, u2 + nextS * tmp);
    saveField(id.x, id.y, id.z, V_FIELD, v1 - upperS * tmp);
    saveField(id.x, id.y + 1, id.z, V_FIELD, v2 + lowerS * tmp);
    saveField(id.x, id.y, id.z, W_FIELD, w1 - frontS * tmp);
    saveField(id.x, id.y, id.z + 1, W_FIELD, w2 + backS * tmp);
}